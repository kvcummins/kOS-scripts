clearscreen.

// Take off

LOCK THROTTLE TO 1.
Print "Engines on...".
STAGE.
PRINT "Throttle up...".
LOCK STEERING TO HEADING(90,0).
WAIT UNTIL SHIP:AIRSPEED > 75.
Print "Rotate...".
LOCK STEERING TO HEADING(90,10).
WAIT UNTIL SHIP:ALTITUDE > 100.
GEAR OFF.
Print "Positive climb, gear up.".

SET EASEOFFALTITUDE TO 20000.

// CLIMB
SET PITCHANGLE TO 20.
LOCK STEERING TO HEADING (90,PITCHANGLE).
LOCAL V0 IS SHIP:AIRSPEED.
LOCAL T0 IS TIME:SECONDS.
UNTIL SHIP:ALTITUDE > EASEOFFALTITUDE {
    WAIT 1.
    SET SHIPACC TO (SHIP:AIRSPEED - V0 ) / (TIME:SECONDS - T0).
    IF SHIPACC < 2 {
        SET PITCHANGLE TO MAX(15,PITCHANGLE-0.5).
        IF SHIPACC < 0 {
            SET PITCHANGLE TO MAX(10,PITCHANGLE-0.1).
        }
    }
    ELSE IF SHIPACC > 5 {
        SET PITCHANGLE TO MIN(30,PITCHANGLE+0.5).
    }
    SET V0 TO SHIP:AIRSPEED.
    SET T0 TO TIME:SECONDS.
    PRINT "Climb with acceleration: " + SHIPACC + " m/s² Pitch: " + PITCHANGLE + "º                       " AT (0,4).
} 

// REACH ORBIT
RCS ON.
UNTIL SHIP:ALTITUDE > 45000 OR SHIP:APOAPSIS > 130000 {
    SET PERCENTCLIMB TO MIN( 1, SHIP:ALTITUDE / 40000).
    IF PITCHANGLE > ARCCOS(PERCENTCLIMB) { SET PITCHANGLE TO ARCCOS(PERCENTCLIMB). }.
    LOCK STEERING TO HEADING(90,PITCHANGLE).
    PRINT "Climb with Pitch: " + PITCHANGLE + "º                       " AT (0,5).
    WAIT 0.
}
WAIT UNTIL SHIP:APOAPSIS > 130000.
PRINT "Apoapsis: " + SHIP:APOAPSIS.
LOCK THROTTLE TO 0.3.
WAIT UNTIL SHIP:APOAPSIS > 150000.
PRINT "Apoapsis: " + SHIP:APOAPSIS.
LOCK THROTTLE TO 0.
WAIT UNTIL SHIP:ALTITUDE > 70000.
STAGE.
UNLOCK STEERING.

PANELS ON.
FUELCELLS ON.
PRINT "Power on".
PRINT "Circularizing...".
RUN CIRC.